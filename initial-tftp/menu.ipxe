#!ipxe

# based on https://gist.github.com/robinsmidsrod/2234639

# Set graphics mode and configure colours
# (picture needs to match resolution to show)
console --x 800 --y 600 --picture ipxe.png --keep
colour --basic 1 --rgb 0xcc2323 1		# red
colour --basic 3 --rgb 0xffaa44 3		# yellow
colour --basic 6 --rgb 0x305796 6		# cyan
cpair --foreground 0 --background 3 2
cpair --foreground 3 3

set esc:hex 1b
set bold ${esc:string}[1m
set orange ${esc:string}[33;0m
set yellow ${esc:string}[33;1m
set blue ${esc:string}[34;1m
set cyan ${esc:string}[36;1m
set resetfg ${esc:string}[39m
set resetbg ${esc:string}[49m
set resetbold ${esc:string}[22m
set reset ${esc:string}[0m

set chk ${bold}
set rshk ${resetbold}
set cname ${orange}

set hostlabel ${hostname}
isset ${hostlabel} || set hostlabel ${ip}

# Variables are specified in boot.ipxe.cfg
isset ${menu-default} || set menu-default exit

###################### MAIN MENU ####################################

:start
menu ${manufacturer} ${product} on ${hostlabel}
item --gap --                MAC ${mac:hexhyp} UUID ${uuid}
item
item --gap --                ----------------------------- Operating systems ---------------------------
item --key u ubuntu          ${chk}U${rshk}buntu 16.04 Desktop
item --gap --                ----------------------------- Submenus ------------------------------------
item --key a submenu-a       Demoprojekt ${chk}A${rshk}
item --gap --                ----------------------------- Tools and utilities -------------------------
item --key l pxelinux        Load ${chk}l${rshk}egacy PXELinux menu
item --gap --         ${blue}----------------------------- Advanced options ----------------------------
item --key 0x137c menu-diag  [${chk}F5${rshk}]  Diagnostics
item --key 0x137e config     [${chk}F7${rshk}]  Configure settings
item --key 0x137f shell      [${chk}F8${rshk}]  Drop to iPXE shell
item reboot                  Reboot computer
item
item --key x exit            E${chk}x${rshk}it iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

############ MAIN MENU ITEMS ############

:pxelinux
chain pxelinux.0 || goto failed
goto start

:ubuntu
echo Starting Ubuntu 16.04 installer
set base-url ${boot-url}ubuntu-16.04-desktop
kernel ${base-url}/linux
initrd ${base-url}/initrd.gz
imgargs linux auto=true fb=false url=${base-url}/./preseed.cfg
boot || goto failed
goto start

:submenu-a
chain --autofree ${boot-dir}a/submenu.ipxe ||
goto start

###################### DIAGNOSTICS MENU #############################

:menu-diag
menu Diagnostic tools
item memtest       Memtest86+ 5.01
item
item --key 0x08 back Back to top menu...
iseq ${menu-default} menu-diag && isset ${submenu-default} && goto menu-diag-timed ||
choose selected && goto ${selected} || goto start
:menu-diag-timed
choose --timeout ${submenu-timeout} --default ${submenu-default} selected && goto ${selected} || goto start

:memtest
chain memtest86plus-5.10.0 || goto failed
goto start




